buildscript {
    ext.kotlin_version = '1.3.30'
    repositories {
        mavenLocal()
        maven { url "https://repo1.maven.org/maven2" }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/releases/"
        }
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.org.gradle.kotlin:gradle-kotlin-dsl-plugins:1.1.3"
        classpath "com.gradle.publish:plugin-publish-plugin:0.11.0"
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.20.0"
        classpath "de.marcphilipp.gradle:nexus-publish-plugin:0.2.0"
    }
}

group 'com.mailchannels.gradle'
// Shared OpenAPI Generator version be passed via command line arg as -PopenApiGeneratorVersion=VERSION
version "$openApiGeneratorVersion"
description = """
This plugin supports common functionality found in Open API Generator CLI as a gradle plugin.

This gives you the ability to generate client SDKs, documentation, new generators, and to validate Open API 2.0 and 3.x
specifications as part of your build. Other tasks are available as command line tasks.
"""
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

apply plugin: 'com.gradle.plugin-publish'
apply plugin: 'java-gradle-plugin'
apply plugin: 'signing'
apply plugin: 'kotlin'
apply plugin: "org.gradle.kotlin.kotlin-dsl"
apply plugin: 'publishing'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenLocal()
    maven { url "https://repo1.maven.org/maven2" }
    maven {
        url "https://oss.sonatype.org/content/repositories/releases/"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    jcenter()

    // this is needed to download "com.mailchannels.gradle:openapi-generator:$openApiGeneratorVersion"
    maven {
        name = 'libs_release'
        credentials {
            username System.getenv('DEPLOYER_USER')
            password System.getenv('DEPLOYER_PASSWORD')
        }
        url 'https://mailchannels.jfrog.io/mailchannels/libs-snapshots-local'
    }
}

dependencies {
    compile gradleApi()
    // Shared OpenAPI Generator version be passed via command line arg as -PopenApiGeneratorVersion=VERSION
    compile "com.mailchannels.openapitools:openapi-generator:$openApiGeneratorVersion"

    // this dependency is needed to build the plugin
    // compile "com.mailchannels.gradle:openapi-generator:$openApiGeneratorVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    testCompile 'org.testng:testng:6.9.6',
            "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    testCompile "org.jetbrains.kotlin:kotlin-compiler-embeddable:$kotlin_version"
}

test {
    useTestNG()
    testClassesDirs = files(project.tasks.compileTestKotlin.destinationDir)
    testLogging.showStandardStreams = false

    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }

    failFast = true

    onOutput { descriptor, event ->
        // SLF4J may complain about multiple bindings depending on how this is run.
        // This is just a warning, but can make test output less readable. So we ignore it specifically.
        if (!event.message.contains("SLF4J:")) {
            logger.lifecycle("Test: " + descriptor + " produced standard out/err: " + event.message)
        }
    }
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            if (project.version.endsWith('-SNAPSHOT'))
                url 'https://mailchannels.jfrog.io/mailchannels/libs-snapshots-local'
            else
                url 'https://mailchannels.jfrog.io/mailchannels/libs-releases-local'

            credentials {
                username = System.getenv('DEPLOYER_USER')
                password = System.getenv('DEPLOYER_PASSWORD')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = 'com.mailchannels.gradle'
            artifactId = 'openapi-generator'
            version = "$openApiGeneratorVersion"

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

gradlePlugin {
    plugins {
        openApiGenerator {
            // this is how the plugin will be applied, it originally was 'org.openapi.generator'
            id = 'com.mailchannels.gradle.openapi-generator'
            implementationClass = 'org.openapitools.generator.gradle.plugin.OpenApiGeneratorPlugin'
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
